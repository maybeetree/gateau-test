project(
  'gateau',
  'cuda', 'cpp', 'c',
  version: '0.2.2',
  default_options: [
    'buildtype=release',
    'cpp_std=c++17',
    'c_std=c17',
  ]
)

#----------------------#
# Find Python          #
#----------------------#

py = import('python').find_installation()


#----------------------#
# Find Compilers       #
#----------------------#

cpp_lang = add_languages('cpp', required: false, native: true)

if not cpp_lang
  error('
    Could not find c++ compiler, aborting gateau installation!
    Please install GCC.
    HINT:
    HINT: sudo apt install gcc
    HINT:
    ')
endif

cuda_lang = add_languages('cuda', required: false, native: true)

if not cuda_lang
  error('
    Could not find nvidia nvcc compiler, aborting gateau installation!
    Please install nvidia\'s CUDA toolkit either from nvidia\'s website 
    or from your distribution\'s package manager.
    NOTE:
    NOTE: Some CUDA packages do not update the PATH environment variable.
    NOTE: Yo must make sure that the directory containing
    NOTE: `nvcc` executable is available in $PATH after installing cuda.
    NOTE:
    ')
endif

#----------------------#
# Find Libraries       #
#----------------------#

cpp = meson.get_compiler('cpp', native: true)

gsl = dependency('gsl', required: false)
gslcblas = cpp.find_library('gslcblas', required: false)
hdf5 = dependency('hdf5', required: false)

cuda_libs = dependency(
  'cuda',
  required: false,
  #version : '>=13',
  modules: ['cufft'],
  )


if not gsl.found()
  error('
    Could not find libgsl, aborting gateau installation!
    Please install pkg-config and 
    GNU Scientific Library with development headers.
    HINT:
    HINT: sudo apt install pkg-config libgsl-dev
    HINT:
    ')
endif

if not gslcblas.found()
  error('
    Could not find gslcblas, aborting gateau installation!
    Please install pkg-config and 
    BLAS component of GNU Scientific Library with development headers.
    NOTE: 
    NOTE: gslcblas is usually packaged with libgsl. 
    NOTE: It is strange that you have gsl but not gslcblas.
    NOTE: Please check your gsl installation.
    NOTE: 
    ')
endif

if not hdf5.found()
  error('
    Could not find hdf5, aborting gateau installation!
    Please install pkg-config and HDF5 with development headers.
    HINT:
    HINT: sudo apt install pkg-config libhdf5-dev
    HINT:
   ' )
endif

if cuda_libs.found()
  message('Meson could find cuda libraries normally, yay!')
else
  warning('Meson could not find CUDA libraries!!')
  warning('Trying random paths until something works...')


  cuda_libs = cpp.find_library(
    'cufft',
    dirs: [
      # Should be here:
      '/usr/local/cuda/lib64',

      # sometimes /usr/local/cuda symlink to
      # specific version doesnt get made,
      # for example on ubuntu cuda11 packages,
      # so check there manually
      '/usr/local/cuda-12/lib64',
      '/usr/local/cuda-12.9/lib64',
      '/usr/local/cuda-12.8/lib64',
      '/usr/local/cuda-12.7/lib64',
      '/usr/local/cuda-12.6/lib64',
      '/usr/local/cuda-12.5/lib64',
      '/usr/local/cuda-12.4/lib64',
      '/usr/local/cuda-12.3/lib64',
      '/usr/local/cuda-12.2/lib64',
      '/usr/local/cuda-12.1/lib64',
      '/usr/local/cuda-11/lib64',
      '/usr/local/cuda-11.9/lib64',
      '/usr/local/cuda-11.8/lib64',
      '/usr/local/cuda-11.7/lib64',
      '/usr/local/cuda-11.6/lib64',
      '/usr/local/cuda-11.5/lib64',
      '/usr/local/cuda-11.4/lib64',
      '/usr/local/cuda-11.3/lib64',
      '/usr/local/cuda-11.2/lib64',
      '/usr/local/cuda-11.1/lib64',

      # Debian official packages
      # (NOT nvidia's packages for debian)
      # put it here:
      '/usr/lib/x86_64-linux-gnu/',

      # Unusual paths, but plausible
      '/lib/x86_64-linux-gnu/',
      '/usr/lib/cuda/lib64/',
      '/usr/lib/cuda/lib64/',
      '/usr/lib/nvidia/',
      '/usr/lib64/cuda/',
      '/usr/local/cuda/lib/',
      '/usr/local/lib/',

      # Probably not here but worth checking
      '/lib64/',
      '/opt/NVIDIA/cuda/lib64/',
      '/opt/cuda/lib64/',
      '/opt/cudnn/lib64/',

      # what
      '/var/cache/cuda/lib64/',
      '/var/spool/cuda/lib64/',
      '~/.local/lib/',
      '~/.local/lib64/',
      '~/cuda/lib64/',
      ],
    required: false
    )

  if not cuda_libs.found()
    error('
      Could not find cufft library.
      Please check your cuda installation.
      If everything looks okay, try installing gateau
      from source (i.e. by cloning repo)
      and making sure that that location of your `libcufft.so`
      is included in the search paths specified in `meson.build`.
      Then, please open an issue.
      ')
  endif
endif

cpp_args = [
  '-Xcompiler',
  '-shared',
  '-fPIC',
]

inc_dirs = include_directories(
  'src/gateau/include',
  'src/gateau/cuda',
)


libs = [cuda_libs, gsl, gslcblas, hdf5]

cu_sources = files(
  'src/gateau/cuda/kernels.cu',
)

libgateau = library(
  'gateau',
  cu_sources,
  include_directories: inc_dirs,
  dependencies: libs,
  name_suffix: 'so',
  pic: true, # no clue, something to do with static linking?
  cpp_args: cpp_args,

  # Install the .so into the gateau python module
  # directory alongside the .py files
  install: true,
  install_dir: py.get_install_dir() / 'gateau',
)

subdir('src/gateau')

